extends Area3D
## https://github.com/godotengine/godot-demo-projects/blob/master/viewport/gui_in_3d/gui_3d.gd

@export var mesh_quad: MeshInstance3D
@export var mesh_color : MeshInstance3D
@export var node_viewport : SubViewport
@onready var node_area : Area3D = self
#var mesh_material: Material

## The last processed input touch/mouse event. Used to calculate relative movement.
var last_event_pos2D := Vector2()

## The time of the last event in seconds since engine start.
var last_event_time := -1.0

## Used for checking if the mouse is inside the Area3D.
var is_mouse_inside: bool = false:
	set(value):
		is_mouse_inside = value
		if mesh_color:
			if is_mouse_inside:
				mesh_color.get_mesh().get_material().set_albedo(Color.GREEN)
				#mesh_material.set_albedo(Color.GREEN)
			else:
				#mesh_material.set_albedo(Color.RED)
				mesh_color.get_mesh().get_material().set_albedo(Color.RED)

func _ready() -> void:
	mouse_entered.connect(_mouse_entered_area)
	mouse_exited.connect(_mouse_exited_area)
	input_event.connect(_mouse_input_event)
	is_mouse_inside = false

func _unhandled_input(event: InputEvent) -> void:
	if node_viewport:
		node_viewport.push_input(event)

func _mouse_entered_area() -> void: 
	is_mouse_inside = true
		# Notify the viewport that the mouse is now hovering it.
	if node_viewport:
		node_viewport.notification(NOTIFICATION_VP_MOUSE_ENTER)

func _mouse_exited_area() -> void:
	is_mouse_inside = false
	# Notify the viewport that the mouse is no longer hovering it.
	if node_viewport:
		node_viewport.notification(NOTIFICATION_VP_MOUSE_EXIT)


func _mouse_input_event(_camera: Camera3D, input_event_: InputEvent, event_position: Vector3, _normal: Vector3, _shape_idx: int) -> void:
	# Get mesh size to detect edges and make conversions. This code only supports PlaneMesh and QuadMesh.
	var quad_mesh_size: Vector2 = mesh_quad.mesh.size

	# Event position in Area3D in world coordinate space.
	var event_pos3D := event_position

	# Current time in seconds since engine start.
	var now := Time.get_ticks_msec() / 1000.0

	# Convert position to a coordinate space relative to the Area3D node.
	# NOTE: `affine_inverse()` accounts for the Area3D node's scale, rotation, and position in the scene!
	event_pos3D = mesh_quad.global_transform.affine_inverse() * event_pos3D

	# TODO: Adapt to bilboard mode or avoid completely.

	var event_pos2D := Vector2()

	if is_mouse_inside:
		# Convert the relative event position from 3D to 2D.
		event_pos2D = Vector2(event_pos3D.x, -event_pos3D.y)

		# Right now the event position's range is the following: (-quad_size/2) -> (quad_size/2)
		# We need to convert it into the following range: -0.5 -> 0.5
		event_pos2D.x = event_pos2D.x / quad_mesh_size.x
		event_pos2D.y = event_pos2D.y / quad_mesh_size.y
		# Then we need to convert it into the following range: 0 -> 1
		event_pos2D.x += 0.5
		event_pos2D.y += 0.5

		# Finally, we convert the position to the following range: 0 -> viewport.size
		event_pos2D.x *= node_viewport.size.x
		event_pos2D.y *= node_viewport.size.y
		# We need to do these conversions so the event's position is in the viewport's coordinate system.

	elif last_event_pos2D != null:
		# Fall back to the last known event position.
		event_pos2D = last_event_pos2D

	# Set the event's position and global position.
	input_event_.position = event_pos2D
	if input_event_ is InputEventMouse:
		input_event_.global_position = event_pos2D

	# Calculate the relative event distance.
	if input_event_ is InputEventMouseMotion or input_event_ is InputEventScreenDrag:
		# If there is not a stored previous position, then we'll assume there is no relative motion.
		if last_event_pos2D == null:
			input_event_.relative = Vector2(0, 0)
		# If there is a stored previous position, then we'll calculate the relative position by subtracting
		# the previous position from the new position. This will give us the distance the event traveled from prev_pos.
		else:
			input_event_.relative = event_pos2D - last_event_pos2D
			input_event_.velocity = input_event_.relative / (now - last_event_time)

	# Update last_event_pos2D with the position we just calculated.
	last_event_pos2D = event_pos2D

	# Update last_event_time to current time.
	last_event_time = now

	# Finally, send the processed input event to the viewport.
	node_viewport.push_input(input_event_)
